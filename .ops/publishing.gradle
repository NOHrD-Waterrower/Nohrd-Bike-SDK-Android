publishing {
    def sonatype_username = "${findProperty("nohrd_sonatype_username") ?: System.getenv("nohrd_sonatype_username")}"
    def sonatype_password = "${findProperty("nohrd_sonatype_password") ?: System.getenv("nohrd_sonatype_password")}"

    repositories {
        maven {
            name = "Sonatype"
            setUrl("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = sonatype_username
                password = sonatype_password
            }
        }

        maven {
            name = "SonatypeSnapshots"
            setUrl("https://oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = sonatype_username
                password = sonatype_password
            }
        }
    }

    task sourcesJar(type: Jar) {
        classifier = "sources"
        from(sourceSets["main"].allSource)
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from(javadoc.destinationDir)
    }

    publications {
        maven(MavenPublication) {
            from(components["java"])
            artifact(sourcesJar)
            artifact(javadocJar)

            version = git.versionName()

            groupId = project.getProperty("groupId")
            artifactId = project.getProperty("artifactId")

            pom {
                name = "Nohrd-Bike-SDK-Android"
                description = "A library for reading data from a NOHrD Bike"
                url = "https://github.com/NOHrD-Waterrower/Nohrd-Bike-SDK-Android"

                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0"
                    }
                }

                developers {
                    developer {
                        id = "nhaarman"
                        name = "Niek Haarman"
                    }
                }

                scm {
                    connection = "scm:git@github.com:NOHrD-Waterrower/Nohrd-Bike-SDK-Android.git"
                    developerConnection = "scm:git@github.com:NOHrD-Waterrower/Nohrd-Bike-SDK-Android.git"
                    url = "https://github.com/NOHrD-Waterrower/Nohrd-Bike-SDK-Android"
                }
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        git.versionName().contains("SNAPSHOT") && repository == publishing.repositories["SonatypeSnapshots"] ||
                !git.versionName().contains("SNAPSHOT") && repository == publishing.repositories["Sonatype"]
    }
}

signing {
    try {
        project.ext."signing.keyId" = nohrd_signing_keyId
        project.ext."signing.password" = nohrd_signing_password
        project.ext."signing.secretKeyRingFile" = nohrd_signing_secretKeyRingFile

        sign(publishing.publications["maven"])
    } catch (Exception e) {
        System.err.println(e.getLocalizedMessage())
        System.err.println("WARNING: Signing not enabled.")
    }
}